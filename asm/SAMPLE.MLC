         PRINT NOGEN
SAMPLE   CSECT
         AMODE 31
         RMODE ANY
         YREGS
***********************************************************************
* HOUSE KEEPING                                                       *
***********************************************************************
         STM   R14,R12,12(R13)
         BALR  R12,0
         USING *,R12
         LA    R15,SAVEAREA
         ST    R15,8(,R13)
         ST    R13,SAVEAREA+4
         LR    R13,R15
***********************************************************************
* MAIN LOGIC                                                          *
***********************************************************************
         WTO   'HELLO WORLD!'
         OPEN  (INFILE,INPUT,OTFILE,OUTPUT)
CLEAR    MVI   OTAREA,X'40'
         MVC   OTAREA+1(131),OTAREA
         GET   INFILE,INAREA
         MVC   LNAME,LAST
         MVC   SALOUT,SALARY
         PUT   OTFILE,OTAREA
         B     CLEAR
EOF      CLOSE (INFILE,,OTFILE)
***********************************************************************
* EXIT                                                                *
***********************************************************************
         L     R13,SAVEAREA+4
         LM    R14,R12,12(R13)
         LHI   R15,0
         BR    R14
***********************************************************************
* DATA AREA                                                           *
***********************************************************************
SAVEAREA DS    18F
*
INFILE   DCB   DDNAME=INFILE,                                          X
               MACRF=GM,                                               X
               RECFM=FT,                                               X  
               BLKSIZE=80,                                             X
               LRECL=80,                                               X
               DSORG=PS,                                               X
               EODAD=EOF
OTFILE   DCB   DDNAME=OTFILE,                                          X
               MACRF=PM,                                               X
               RECFM=FT,                                               X
               BLKSIZE=132,                                            X
               LRECL=132,                                              X
               DSORG=PS
INAREA   DS    0CL80
LAST     DS    CL15
SALARY   DS    CL5
         DS    CL60
OTAREA   DS    0CL132
         DS    CL5
LNAME    DS    CL15
         DS    CL20
SALOUT   DS    CL5
         DS    CL87
         LTORG
         END
